---
title: Interchain Messaging での送信元確認  
description: メッセージの送信元を安全に検証する  
updated: 2024-05-31  
authors: [martineckardt]  
icon: BookOpen  
---

Interchain Messaging のレジストリを活用することで、`msg.sender` が登録済みの Interchain Messaging コントラクトであるかを確認できます。  

これまでは、コントラクト内でハードコードして送信元の検証を行っていました。  

```solidity
import "@teleporter/ITeleporterMessenger.sol";
import "@teleporter/ITeleporterReceiver.sol";

contract ReceiverOnSubnet is ITeleporterReceiver {
    ITeleporterMessenger public immutable messenger = ITeleporterMessenger(0x253b2784c75e510dD0fF1da844684a1aC0aa5fcf);

    string public lastMessage;

    function receiveTeleporterMessage(bytes32, address, bytes calldata message) external {
        // Only the Interchain Messaging receiver can deliver a message. // [!code highlight:2]
        require(msg.sender == address(messenger), "ReceiverOnSubnet: unauthorized TeleporterMessenger");

        // Store the message.
        lastMessage = abi.decode(message, (string));
    }
}
```

しかし、新しいバージョンの Interchain Messaging コントラクトが導入された場合、  
このコントラクトも都度更新しなければなりません。

### **レジストリを利用した送信元の検証**
これを解決するために、レジストリを活用し、送信元アドレスが登録済みの Interchain Messaging コントラクトであるかをチェックできます。

```solidity
pragma solidity ^0.8.18;

import "@teleporter/upgrades/TeleporterRegistry.sol";
import "@teleporter/ITeleporterMessenger.sol";
import "@teleporter/ITeleporterReceiver.sol";

contract ReceiverOnDispatchWithRegistry is ITeleporterReceiver {
    // The Interchain Messaging registry contract manages different Interchain Messaging contract versions.
    TeleporterRegistry public immutable teleporterRegistry =
        TeleporterRegistry(0x827364Da64e8f8466c23520d81731e94c8DDe510);

    string public lastMessage;

    function receiveTeleporterMessage(bytes32, address, bytes calldata message) external {
        // Only a Interchain Messaging Messenger registered in the registry can deliver a message. // [!code highlight:3]
        // Function throws an error if msg.sender is not registered.
        teleporterRegistry.getVersionFromAddress(msg.sender);

        // Store the message.
        lastMessage = abi.decode(message, (string));
    }
}
```

### **このコードのポイント**
- `TeleporterRegistry` を利用して、Interchain Messaging のバージョン管理を行う  
- **`msg.sender` がレジストリに登録されたアドレスかどうかを動的に検証** する  
- **新しいバージョンの Interchain Messaging コントラクトが導入されても、レジストリに登録されていれば問題なく動作** する  

このように、レジストリを活用することで **柔軟で保守性の高いコントラクト設計** が可能になります。