---
title: 複数チェーンにわたる VRF リクエストのオーケストレーション（Wrapper）  
description: Chainlink をサポートする L1 上の `CrossChainVRFWrapper` コントラクトが、クロスチェーン VRF リクエストを処理する仕組みを学ぶ。  
updated: 2024-10-21  
authors: [0xstt]  
icon: BookOpen  
---

import { Step, Steps } from 'fumadocs-ui/components/steps';  

`CrossChainVRFWrapper` コントラクトは、Chainlink VRF を直接サポートしていない L1 からのリクエストを処理する重要な役割を果たします。  

このコントラクトは **Chainlink をサポートするネットワーク**（例：Avalanche Fuji）上にデプロイされ、Chainlink VRF と直接やり取りすることで、ランダム数のリクエストを処理します。  

ここでは、`CrossChainVRFWrapper` がどのように VRF サービスを提供するのかを説明します。  

## クロスチェーンメッセージの受信  

<Steps>  
<Step>  

`CrossChainVRFConsumer`（VRF をリクエストするコントラクト）が、Chainlink をサポートしていない L1 上でランダム数のリクエストを開始します。このリクエストは、`TeleporterMessenger` を介して `CrossChainVRFWrapper` に送信されます。  

</Step>  
<Step>  

`CrossChainVRFWrapper` は、リクエストが承認されたアドレスからのものであることを検証します。これにより、未承認のコントラクトやアドレスがランダム数をリクエストできないようにします。  

</Step>  
<Step>  

リクエストが正当であることを確認した後、**VRFWrapper** は **Chainlink VRF** にリクエストを送信し、ランダム数を要求します。この際、リクエストには必要なパラメータ（ランダム数の個数、確認回数、ガスリミットなど）が含まれます。  

</Step>  
<Step>  

`CrossChainVRFWrapper` は、すべての保留中のリクエストをマッピングして追跡し、各リクエスト ID を `CrossChainVRFConsumer` のある L1 に関連付けます。これにより、リクエストが完了した際に、正しいチェーンへランダム数を送信できます。  

</Step>  
</Steps>  

<Accordions>  
<Accordion title="実装">  

```solidity
function receiveTeleporterMessage(
    bytes32 originChainID,
    address originSenderAddress,
    bytes calldata message
) external {
    require(msg.sender == address(teleporterMessenger), "Caller is not the TeleporterMessenger");
    // Verify that the origin sender address is authorized
    require(authorizedSubscriptions[originSenderAddress].isAuthorized, "Origin sender is not authorized");
    uint256 subscriptionId = authorizedSubscriptions[originSenderAddress].subscriptionId;
    // Verify that the subscription ID belongs to the correct owner
    (,,,, address[] memory consumers) = s_vrfCoordinator.getSubscription(subscriptionId);
    // Check wrapper contract is a consumer of the subscription
    bool isConsumer = false;
    for (uint256 i = 0; i < consumers.length; i++) {
        if (consumers[i] == address(this)) {
            isConsumer = true;
            break;
        }
    }
    require(isConsumer, "Contract is not a consumer of this subscription");
    // Decode message to get the VRF parameters
    CrossChainRequest memory vrfMessage = abi.decode(message, (CrossChainRequest));
    // Request random words
    VRFV2PlusClient.RandomWordsRequest memory req = VRFV2PlusClient.RandomWordsRequest({
        keyHash: vrfMessage.keyHash,
        subId: subscriptionId,
        requestConfirmations: vrfMessage.requestConfirmations,
        callbackGasLimit: vrfMessage.callbackGasLimit,
        numWords: vrfMessage.numWords,
        extraArgs: VRFV2PlusClient._argsToBytes(VRFV2PlusClient.ExtraArgsV1({nativePayment: vrfMessage.nativePayment}))
    });
    uint256 requestId = s_vrfCoordinator.requestRandomWords(req);
    pendingRequests[requestId] = CrossChainReceiver({
        destinationBlockchainId: originChainID,
        destinationAddress: originSenderAddress
    });
}   
```

</Accordion>  
</Accordions>  

## Chainlink VRF のコールバック処理  

<Steps>  
<Step>  

**Chainlink VRF** がランダム数のリクエストを処理すると、`CrossChainVRFWrapper` はコールバック関数を介してランダム数を受け取ります。これにより、リクエストが正常に処理されたことが保証されます。  

</Step>  
<Step>  

ランダム数を受信した後、`CrossChainVRFWrapper` はその結果をエンコードし、クロスチェーンメッセージとして `CrossChainVRFConsumer` に送信します。このメッセージの送信には `TeleporterMessenger` を使用します。  

</Step>  
</Steps>  

<Accordions>  
<Accordion title="実装">  

```solidity
function fulfillRandomWords(uint256 requestId, uint256[] calldata randomWords) internal override {
    require(pendingRequests[requestId].destinationAddress != address(0), "Invalid request ID");
    // Create CrossChainResponse struct
    CrossChainResponse memory crossChainResponse = CrossChainResponse({
        requestId: requestId,
        randomWords: randomWords
    });
    bytes memory encodedMessage = abi.encode(crossChainResponse);
    // Send cross chain message using ITeleporterMessenger interface
    TeleporterMessageInput memory messageInput = TeleporterMessageInput({
        destinationBlockchainID: pendingRequests[requestId].destinationBlockchainId,
        destinationAddress: pendingRequests[requestId].destinationAddress,
        feeInfo: TeleporterFeeInfo({ feeTokenAddress: address(0), amount: 0 }),
        requiredGasLimit: 100000,
        allowedRelayerAddresses: new address[](0),
        message: encodedMessage
    });
    teleporterMessenger.sendCrossChainMessage(messageInput);
    delete pendingRequests[requestId];
}
```

</Accordion>  
</Accordions>